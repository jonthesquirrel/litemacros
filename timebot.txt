UNSET(&args)
SPLIT(" ",$$[1],&args)
IF(!@&timebot_prefix)
    @&timebot_prefix = "<timebot>"
ENDIF

IF(@#timebot_time > 0)
    UNSET(#min)
    UNSET(#sec)
    //modulo
    #min = ((@#timebot_time - (@#timebot_time-(60*(@#timebot_time/60)))) / 60)
    #sec = (@#timebot_time-(60*(@#timebot_time/60)))
    &human_time = ""
    IF(#min != 0)
        &human_time = "%&human_time%%#min%m"
    ENDIF
    IF(#sec != 0)
        &human_time = "%&human_time%%#sec%s"
    ENDIF
ELSE
    &human_time = "0s"
ENDIF

IF(&args[0] = "set")
    UNSET(#min)
    UNSET(#sec)
    MATCH(%&args[1]%,"(\d+)m",#min,1)
    MATCH(%&args[1]%,"(\d+)s",#sec,1)
    @#timebot_time = (#min * 60) + #sec
    LOG("&r%@&timebot_prefix% set to:")
    EXEC("timebot.txt","timebot","get")
ELSEIF(&args[0] = "start")
    SET(@timebot_active)
    STOP("timebot_d")
    EXEC("timebot_d.txt","timebot_d")
    LOG("&r%@&timebot_prefix% started")
ELSEIF(&args[0] = "stop")
    UNSET(@timebot_active)
    STOP("timebot_d")
    LOG("&r%@&timebot_prefix% stopped")
ELSEIF(&args[0] = "end")
    @#timebot_time = 0
    LOG("&r%@&timebot_prefix% ended")
ELSEIF(&args[0] = "add")
    UNSET(#min)
    UNSET(#sec)
    MATCH(%&args[1]%,"(\d+)m",#min,1)
    MATCH(%&args[1]%,"(\d+)s",#sec,1)
    #temp_time = (#min * 60) + #sec
    INC(@#timebot_time,%#temp_time%)
    LOG("&r%@&timebot_prefix% added %&args[1]%, now is:")
    EXEC("timebot.txt","timebot","get")
ELSEIF(&args[0] = "rem")
    UNSET(#min)
    UNSET(#sec)
    MATCH(%&args[1]%,"(\d+)m",#min,1)
    MATCH(%&args[1]%,"(\d+)s",#sec,1)
    #temp_time = (#min * 60) + #sec
    IF(#temp_time < @#timebot_time)
        DEC(@#timebot_time,%#temp_time%)
    ELSE
        @#timebot_time = 0
    ENDIF
    LOG("&r%@&timebot_prefix% removed %&args[1]%, now is:")
    EXEC("timebot.txt","timebot","get")
ELSEIF(&args[0] = "end")
    @#timebot_time = 0
ELSEIF(&args[0] = "say")
    IF(@timebot_active)
        ECHO("%@&timebot_prefix% %&human_time%")
    ELSE
        ECHO("%@&timebot_prefix% %&human_time% (paused)")
    ENDIF
ELSEIF(&args[0] = "get")
    IF(@timebot_active)
        LOG("&r%@&timebot_prefix% %&human_time%")
    ELSE
        LOG("&r%@&timebot_prefix% %&human_time% (paused)")
    ENDIF
ELSEIF(&args[0] = "prefix")
    IF(&args[1])
        IF(&args[1] = "reset")
            @&timebot_prefix = "<timebot>"
            LOG("&r%@&timebot_prefix% <- prefix reset")
        ELSE
            @&timebot_prefix = "%&args[1]%"
            LOG("&r%@&timebot_prefix% <- prefix set")
        ENDIF
    ELSE
        LOG("&r%@&timebot_prefix% <- prefix")
    ENDIF
ELSEIF(&args[0] = "cmd")
    //JOIN(" ",&args[1-max],@&timebot_cmd)
    //if [-]cmd then EXEC("timebot_cmd_[cmd].txt","timebot_cmd")
ELSE
    LOG("<timebot> usage: /!timebot <<set|add|rem> <Xm|Xs|XmXs>|stop|start|end|say|get|prefix [<prefix>|reset]|cmd [ending_command|-file]>")
ENDIF
