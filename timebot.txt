UNSET(&args)
SPLIT(" ",$$[1],&args)
IF(!@&timebot_prefix)
    @&timebot_prefix = "<timebot>"
ENDIF

IF(@#timebot_time > 0)
    UNSET(#min)
    UNSET(#sec)
    //modulo
    #min = ((@#timebot_time - (@#timebot_time-(60*(@#timebot_time/60)))) / 60)
    #sec = (@#timebot_time-(60*(@#timebot_time/60)))
    &human_time = ""
    IF(#min != 0)
        &human_time = "%&human_time%%#min%m"
    ENDIF
    IF(#sec != 0)
        &human_time = "%&human_time%%#sec%s"
    ENDIF
ELSE
    &human_time = "0s"
ENDIF

IF(&args[0] = "set")
    UNSET(#min)
    UNSET(#sec)
    MATCH(%&args[1]%,"(\d+)m",#min,1)
    MATCH(%&args[1]%,"(\d+)s",#sec,1)
    @#timebot_time = (#min * 60) + #sec
    LOG("&r%@&timebot_prefix% set to:")
    EXEC("timebot.txt","timebot","stat")
ELSEIF(&args[0] = "start")
    STOP("timebot_d")
    SET(@timebot_active)
    EXEC("timebot_d.txt","timebot_d")
    LOG("&r%@&timebot_prefix% started")
ELSEIF(&args[0] = "stop")
    UNSET(@timebot_active)
    STOP("timebot_d")
    LOG("&r%@&timebot_prefix% stopped")
ELSEIF(&args[0] = "add")
    UNSET(#min)
    UNSET(#sec)
    MATCH(%&args[1]%,"(\d+)m",#min,1)
    MATCH(%&args[1]%,"(\d+)s",#sec,1)
    #temp_time = (#min * 60) + #sec
    INC(@#timebot_time,%#temp_time%)
    LOG("&r%@&timebot_prefix% added %&args[1]%, now is:")
    EXEC("timebot.txt","timebot","stat")
ELSEIF(&args[0] = "rem")
    UNSET(#min)
    UNSET(#sec)
    MATCH(%&args[1]%,"(\d+)m",#min,1)
    MATCH(%&args[1]%,"(\d+)s",#sec,1)
    #temp_time = (#min * 60) + #sec
    IF(#temp_time < @#timebot_time)
        DEC(@#timebot_time,%#temp_time%)
    ELSE
        @#timebot_time = 0
    ENDIF
    LOG("&r%@&timebot_prefix% removed %&args[1]%, now is:")
    EXEC("timebot.txt","timebot","stat")
ELSEIF(&args[0] = "end")
    LOG("&r%@&timebot_prefix% ending...")
    @#timebot_time = 0
    EXEC("timebot.txt","timebot","start")
ELSEIF(&args[0] = "say")
    IF(@timebot_active)
        ECHO("%@&timebot_prefix% %&human_time%")
    ELSE
        ECHO("%@&timebot_prefix% %&human_time% (paused)")
    ENDIF
ELSEIF(&args[0] = "stat")
    &str = "&r%@&timebot_prefix%"
    &str = "%&str% %&human_time%"
    IF(!@timebot_active)
        &str = "%&str% (paused)"
    ENDIF
    IF(@timebot_mute)
        &str = "%&str% [muted]"
    ENDIF
    IF(@&timebot_cmd)
        &str = "%&str% [cmd: %@&timebot_cmd% ]"
    ENDIF

    LOG("%&str%")
ELSEIF(&args[0] = "mute")
    IF(&args[1] = "on")
        SET(@timebot_mute)
        LOG("&r%@&timebot_prefix% mute set on")
    ELSEIF(&args[1] = "off")
        UNSET(@timebot_mute)
        LOG("&r%@&timebot_prefix% mute set off")
    ELSE
        IF(@timebot_mute)
            LOG("&r%@&timebot_prefix% mute is on")
        ELSE
            LOG("&r%@&timebot_prefix% mute is off")
        ENDIF
    ENDIF
ELSEIF(&args[0] = "prefix")
    IF(&args[1])
        @&timebot_prefix = "%&args[1]%"
        LOG("&r%@&timebot_prefix% <- prefix set")
    ELSE
        @&timebot_prefix = "<timebot>"
        LOG("&r%@&timebot_prefix% <- prefix reset")
    ENDIF
ELSEIF(&args[0] = "cmd")
    IF(&args[1])
        UNSET(&args[0])
        JOIN(" ",&args,@&timebot_cmd)
        LOG("&r%@&timebot_prefix% command set: %@&timebot_cmd%")
    ELSE
        UNSET(@&timebot_cmd)
        LOG("&r%@&timebot_prefix% command reset")
    ENDIF
ELSE
    LOG("&r%@&timebot_prefix% usage: /!timebot <set|add|rem> <Xm|Xs|XmXs>|stop|start|end|say|stat|mute [on|off]|prefix [prefix]|cmd [command|-file]")
ENDIF
